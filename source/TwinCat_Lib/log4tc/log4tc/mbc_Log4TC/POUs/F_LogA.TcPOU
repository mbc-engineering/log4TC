<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="F_LogA" Id="{8ac1e49b-4f8b-43a6-ae63-dd568afe353d}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'conditionalshow'}
FUNCTION INTERNAL F_LogA 
VAR_INPUT
	eLogLevel		: E_LogLevel;
	sLogger			: T_MaxString;
	sMessage		: T_MaxString;
	nArgCount		: UINT (0..10);
	aArg1			: ANY;
	aArg2			: ANY;
	aArg3			: ANY;
	aArg4			: ANY;
	aArg5			: ANY;
	aArg6			: ANY;
	aArg7			: ANY;
	aArg8			: ANY;
	aArg9			: ANY;
	aArg10			: ANY;
	iContextBuilder	: I_ContextBuilder;
END_VAR
VAR
	fbLogEntry			: FB_LogEntry();
	pBuffer				: PVOID;
	nBufferCount		: UINT;	
	iLogEntryAdder		: I_LogEntryAdder;
	nIdx				: UINT;
	refScopedContext	: REFERENCE TO ST_ScopedContextStack;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[fbLogEntry.Start(
	pLogger			:= ADR(sLogger),
	pMessage		:= ADR(sMessage),
	eLogLevel		:= eLogLevel,
	nTimeStampPlc	:= 0,
	nTimeStampClock	:= 0
);

IF nArgCount >= 1 THEN
	fbLogEntry.AddAnyArg(1, aArg1);
END_IF
IF nArgCount >= 2 THEN 
	fbLogEntry.AddAnyArg(2, aArg2);
END_IF
IF nArgCount >= 3 THEN 
	fbLogEntry.AddAnyArg(3, aArg3);
END_IF
IF nArgCount >= 4 THEN 
	fbLogEntry.AddAnyArg(4, aArg4);
END_IF
IF nArgCount >= 5 THEN 
	fbLogEntry.AddAnyArg(5, aArg5);
END_IF
IF nArgCount >= 6 THEN 
	fbLogEntry.AddAnyArg(6, aArg6);
END_IF
IF nArgCount >= 7 THEN 
	fbLogEntry.AddAnyArg(7, aArg7);
END_IF
IF nArgCount >= 8 THEN 
	fbLogEntry.AddAnyArg(8, aArg8);
END_IF
IF nArgCount >= 9 THEN 
	fbLogEntry.AddAnyArg(9, aArg9);
END_IF
IF nArgCount >= 10 THEN 
	fbLogEntry.AddAnyArg(10, aArg10);
END_IF

IF __QUERYINTERFACE(PRG_TaskLog.TaskLogger.TaskContext, iLogEntryAdder) THEN
	iLogEntryAdder.AddToLogEntry(E_Scope.eTask, fbLogEntry);
END_IF

refScopedContext REF= PRG_ScopedContextStack.TaskContextStack;
IF refScopedContext.nScopeContextCount > 0 THEN
	FOR nIdx := 0 TO refScopedContext.nScopeContextCount - 1 DO
		IF __QUERYINTERFACE(refScopedContext.aScopedContext[nIdx], iLogEntryAdder) THEN
			iLogEntryAdder.AddToLogEntry(E_Scope.eScoped, fbLogEntry);
		END_IF
	END_FOR
END_IF

IF iContextBuilder <> 0 AND __QUERYINTERFACE(iContextBuilder, iLogEntryAdder) THEN
	iLogEntryAdder.AddToLogEntry(E_Scope.eLogger, fbLogEntry);
END_IF

fbLogEntry.End();
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>