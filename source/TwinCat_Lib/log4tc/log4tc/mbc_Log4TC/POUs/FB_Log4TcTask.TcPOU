<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.0">
  <POU Name="FB_Log4TcTask" Id="{7a1ab82d-e3cd-4e48-96e0-5ca76f3c5915}" SpecialFunc="None">
    <Declaration><![CDATA[(*
Main code of Log4TC which manages log messages of a single task.
*)
FUNCTION_BLOCK INTERNAL FB_Log4TcTask
VAR
	nTaskIndex			: DINT;
	fbRingBuffer		: Tc2_Utilities.FB_MemRingBufferEx;
	aBuffer				: ARRAY[1..Config.nBufferLen] OF BYTE;
	fbTaskContext		: FB_ContextBuilder;
END_VAR]]></Declaration>
    <Implementation>
      <CFC>
        <XmlArchive>
          <Data>
            <o xml:space="preserve" t="CFCImplementationObject">
              <o n="Items" t="CFCItemList">
                <l2 n="InnerList" />
              </o>
              <n n="ParameterInitializationMethodGenerator" />
              <o n="RoutingPathTable" t="CFCRoutingPathTable">
                <d2 n="InnerDictionary" />
              </o>
              <v n="AutoSizeCanvas">true</v>
              <v n="CanvasWidth">1</v>
              <v n="CanvasHeight">1</v>
            </o>
          </Data>
          <TypeList>
            <Type n="Boolean">System.Boolean</Type>
            <Type n="CFCImplementationObject">{32d3375e-c010-41e2-9e43-b2fbf4f2b374}</Type>
            <Type n="CFCItemList">{cd57ba20-558b-4b98-96c1-73c6000c3087}</Type>
            <Type n="CFCRoutingPathTable">{4b8bcc79-5980-4868-b49e-005a8148859b}</Type>
            <Type n="Int32">System.Int32</Type>
          </TypeList>
        </XmlArchive>
      </CFC>
    </Implementation>
    <Method Name="_InitAndCheckTask" Id="{cf1c3c00-c377-4ffb-a1eb-30fbb2bc0114}">
      <Declaration><![CDATA[METHOD PRIVATE _InitAndCheckTask : BOOL
VAR
	nActTaskIndex		: DINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[nActTaskIndex := Tc2_System.GETCURTASKINDEXEX();

IF nTaskIndex = 0 AND nActTaskIndex > 0 THEN
	nTaskIndex := nActTaskIndex;
	_InitAndCheckTask := TRUE;
ELSIF nTaskIndex <> nActTaskIndex OR nActTaskIndex = 0 THEN
	F_InternalLog(
		E_LogLevel.eFatal,
		'Log4TcTask._InitAndCheckTask',
		'Log4Tc called in wrong task (expected=%d actual=%d)',
		F_DINT(nTaskIndex),
		F_DINT(nActTaskIndex),
		Tc2_Utilities.EMPTY_ARG_VALUE,
		Tc2_Utilities.EMPTY_ARG_VALUE
	);
	_InitAndCheckTask := FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddLog" Id="{a60c1ca6-dc51-4ca0-8f1b-e9265e4c8b3d}">
      <Declaration><![CDATA[METHOD INTERNAL AddLog : BOOL
VAR_INPUT
	pLogBuffer		: PVOID;
	nLogBufferLen	: UINT;
END_VAR
VAR
	nActTaskIndex		: DINT;
	bAddOk			: BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _InitAndCheckTask() THEN
	RETURN;
END_IF

fbRingBuffer.A_AddTail(
	pBuffer 	:= ADR(aBuffer),
	cbBuffer 	:= SIZEOF(aBuffer),
	pWrite		:= pLogBuffer,
	cbWrite		:= nLogBufferLen,	
	bOk			=> bAddOk,
);

IF NOT bAddOk THEN
	F_InternalLog(
		E_LogLevel.eFatal,
		'Log4TcTask.AddLog',
		'Error adding log message to buffer. TaskIdx=%d MessageSize=%d BufferFree=%d',
		Tc2_Utilities.F_DINT(nTaskIndex),
		Tc2_Utilities.F_UINT(nLogBufferLen),
		Tc2_Utilities.F_UDINT(fbRingBuffer.cbFree),
		Tc2_Utilities.EMPTY_ARG_VALUE
	);
	
	IncUDINT(Log4TcInfo.aTaskInfo[nTaskIndex].nAddLogToBufferErrorCount);
	IF Log4TcInfo.aTaskInfo[nTaskIndex].nMaxUsedBufferSize < fbRingBuffer.cbSize THEN
		Log4TcInfo.aTaskInfo[nTaskIndex].nMaxUsedBufferSize := fbRingBuffer.cbSize;
	END_IF
	IF Log4TcInfo.aTaskInfo[nTaskIndex].nMaxLogsInBuffer <= fbRingBuffer.nCount THEN
		Log4TcInfo.aTaskInfo[nTaskIndex].nMaxLogsInBuffer := UDINT_TO_UINT(fbRingBuffer.nCount);
	END_IF
ELSE
	F_InternalLog(
		E_LogLevel.eTrace,
		'Log4TcTask.AddLog',
		'Adding log message to buffer. TaskIdx=%d MessageSize=%d BufferFree=%d',
		Tc2_Utilities.F_DINT(nTaskIndex),
		Tc2_Utilities.F_UINT(nLogBufferLen),
		Tc2_Utilities.F_UDINT(fbRingBuffer.cbFree),
		Tc2_Utilities.EMPTY_ARG_VALUE
	);	
END_IF 

AddLog := bAddOk;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Call" Id="{ef6225c0-33b1-4834-a78e-390eae37cdd9}">
      <Declaration><![CDATA[(*
Should be called regularly in the same task as its instance was
declared.
*)
METHOD INTERNAL Call
VAR_INPUT
	sAmsNetId			: Tc2_System.T_AmsNetID;
END_VAR
VAR
	nMessageCount		: UINT;
END_VAR
VAR_INST
	nState				: UINT;
	aSendBuffer			: ARRAY[1..Config.nBufferLen] OF BYTE;		(* Buffer for send data *)
	nSendBufferLen		: UDINT;
	bContinue			: BOOL;
	fbAdsWriteMsg		: Tc2_System.ADSWRITE:=(PORT:=Const.nAdsPort);
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _InitAndCheckTask() THEN
	RETURN;
END_IF

CASE nState OF
	0: // Init
		nSendBufferLen := 0;
		nState := 10;
		
	10: // Wait for log entry; add them to the send buffer
		nMessageCount := 0;
		REPEAT
			fbRingBuffer.A_GetHead(
				pBuffer 	:= ADR(aBuffer),
				cbBuffer 	:= SIZEOF(aBuffer),
			);
			
			IF fbRingBuffer.bOk AND (SIZEOF(aSendBuffer) - nSendBufferLen) >= fbRingBuffer.cbRead THEN
				MEMCPY(ADR(aSendBuffer) + nSendBufferLen, fbRingBuffer.pRead, fbRingBuffer.cbRead);
				nSendBufferLen := nSendBufferLen + fbRingBuffer.cbRead;
				
				fbRingBuffer.A_FreeHead(
					pBuffer 	:= ADR(aBuffer),
					cbBuffer 	:= SIZEOF(aBuffer),
				);
				
				nMessageCount := nMessageCount + 1;
				bContinue := TRUE;
			ELSE
				bContinue := FALSE;
			END_IF
		UNTIL
			NOT bContinue
		END_REPEAT;

		IF nSendBufferLen > 0 THEN
			F_InternalLog(
				E_LogLevel.eDebug,
				'FB_Log4TcTask.Call',
				'Sending %d message with size %d.',
				Tc2_Utilities.F_UINT(nMessageCount),
				Tc2_Utilities.F_UDINT(nSendBufferLen),
				Tc2_Utilities.EMPTY_ARG_VALUE,
				Tc2_Utilities.EMPTY_ARG_VALUE
			);	
		
			fbAdsWriteMsg(WRITE:=FALSE);
			fbAdsWriteMsg(
				WRITE	:= TRUE,
				NETID	:= sAmsNetId,
				IDXGRP 	:= Const.nLogIndexGrp,
				IDXOFFS := Const.nLogIndexOffs,
				LEN		:= nSendBufferLen,
				SRCADDR	:= ADR(aSendBuffer),
			);
		
			nState := 20;
		END_IF
		
	20: // Wait for sent data
		fbAdsWriteMsg();
		IF NOT fbAdsWriteMsg.BUSY THEN
			IF fbAdsWriteMsg.ERR THEN
				F_InternalLog(
					E_LogLevel.eFatal,
					'Log4TcTask.Call',
					'Error sending log message to handler. task=%d adsErrId=%d',
					F_DINT(nTaskIndex),
					F_UDINT(fbAdsWriteMsg.ERRID),
					Tc2_Utilities.EMPTY_ARG_VALUE,
					Tc2_Utilities.EMPTY_ARG_VALUE
				);
				
				IncUDINT(Log4TcInfo.aTaskInfo[nTaskIndex].nSendLogBufferErrorCount);
			ELSE
				F_InternalLog(
					E_LogLevel.eDebug,
					'FB_Log4TcTask.Call',
					'Message successfully sent.',
					Tc2_Utilities.EMPTY_ARG_VALUE,
					Tc2_Utilities.EMPTY_ARG_VALUE,
					Tc2_Utilities.EMPTY_ARG_VALUE,
					Tc2_Utilities.EMPTY_ARG_VALUE
				);	
			END_IF
			
			nSendBufferLen := 0;
			nState := 10;
		END_IF
	
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{f1e448f2-18e5-42ea-bc52-6c978df9ed18}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbRingBuffer.A_Reset
(
	pBuffer 	:= ADR(aBuffer),
	cbBuffer 	:= SIZEOF(aBuffer),
);

	]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Log4TcTask._InitAndCheckTask">
      <LineId Id="6" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="7" Count="1" />
      <LineId Id="23" Count="0" />
      <LineId Id="9" Count="10" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Log4TcTask.AddLog">
      <LineId Id="81" Count="1" />
      <LineId Id="62" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="11" Count="1" />
      <LineId Id="15" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="37" Count="7" />
      <LineId Id="22" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="92" Count="0" />
      <LineId Id="94" Count="5" />
      <LineId Id="48" Count="0" />
      <LineId Id="50" Count="7" />
      <LineId Id="49" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="18" Count="0" />
    </LineIds>
    <LineIds Name="FB_Log4TcTask.Call">
      <LineId Id="107" Count="0" />
      <LineId Id="202" Count="1" />
      <LineId Id="12" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="20" Count="1" />
      <LineId Id="160" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="28" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="36" Count="1" />
      <LineId Id="39" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="40" Count="0" />
      <LineId Id="42" Count="1" />
      <LineId Id="41" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="161" Count="0" />
      <LineId Id="47" Count="2" />
      <LineId Id="38" Count="0" />
      <LineId Id="51" Count="4" />
      <LineId Id="162" Count="0" />
      <LineId Id="165" Count="6" />
      <LineId Id="164" Count="0" />
      <LineId Id="163" Count="0" />
      <LineId Id="58" Count="0" />
      <LineId Id="62" Count="1" />
      <LineId Id="201" Count="0" />
      <LineId Id="65" Count="3" />
      <LineId Id="64" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="56" Count="1" />
      <LineId Id="69" Count="4" />
      <LineId Id="118" Count="5" />
      <LineId Id="125" Count="2" />
      <LineId Id="223" Count="0" />
      <LineId Id="222" Count="0" />
      <LineId Id="172" Count="0" />
      <LineId Id="174" Count="7" />
      <LineId Id="173" Count="0" />
      <LineId Id="76" Count="0" />
      <LineId Id="88" Count="0" />
      <LineId Id="86" Count="1" />
      <LineId Id="74" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="14" Count="0" />
    </LineIds>
    <LineIds Name="FB_Log4TcTask.FB_init">
      <LineId Id="7" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="8" Count="1" />
      <LineId Id="11" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="10" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>